AC_INIT
AC_CONFIG_SRCDIR([test/test.c])
PACKAGE=libxls

LIBXLS_MAJOR_VERSION=1
LIBXLS_MINOR_VERSION=0
LIBXLS_MICRO_VERSION=0

VERSION=$LIBXLS_MAJOR_VERSION.$LIBXLS_MINOR_VERSION.$LIBXLS_MICRO_VERSION
MAJOR_VERSION_PLUS_MINOR_VERSION=1
VERSION_INFO=$MAJOR_VERSION_PLUS_MINOR_VERSION:$LIBXLS_MICRO_VERSION:$LIBXLS_MINOR_VERSION

AC_SUBST(VERSION_INFO)
AC_SUBST(LIBXLS_MAJOR_VERSION)
AC_SUBST(LIBXLS_MINOR_VERSION)
AC_SUBST(LIBXLS_MICRO_VERSION)

AM_INIT_AUTOMAKE($PACKAGE,$VERSION)

AC_CONFIG_HEADERS([config.h])
AC_PREFIX_DEFAULT(/usr/local/libxls)
AC_PROG_INSTALL

AC_PROG_CC
AC_PROG_CXX
AM_PROG_LIBTOOL
AC_SUBST(LIBTOOL_DEPS)


AC_PATH_PROG(PKG_CONFIG, pkg-config, no)

AC_PROG_INSTALL	        

AC_CHECK_FUNCS([pow])
AC_CHECK_FUNCS([strdup])
AC_CHECK_HEADERS([netinet/in.h])
AC_CHECK_HEADERS([wchar.h])
AC_C_CONST
AC_FUNC_MALLOC
AC_FUNC_REALLOC
AC_PROG_RANLIB
AC_TYPE_SIZE_T

WITH_ICONV=0
AC_ARG_WITH(iconv, [  --with-iconv[=DIR]        add ICONV support (on)])
if test "$with_iconv" = "no" ; then
    echo Disabling ICONV support
else
    if test "$with_iconv" != "yes" -a "$with_iconv" != "" ; then
	CPPFLAGS="${CPPFLAGS} -I$with_iconv/include"
	# Export this since our headers include iconv.h
	XML_INCLUDEDIR="${XML_INCLUDEDIR} -I$with_iconv/include"
	ICONV_LIBS="-L$with_iconv/lib"
    fi

    AC_CHECK_HEADER(iconv.h,
	AC_MSG_CHECKING(for iconv)
	AC_TRY_LINK([#include <stdlib.h>
#include <iconv.h>],[
iconv_t cd = iconv_open ("","");
iconv (cd, NULL, NULL, NULL, NULL);],[
	    AC_MSG_RESULT(yes)
	    WITH_ICONV=1],[
	    AC_MSG_RESULT(no)
	    AC_MSG_CHECKING(for iconv in -liconv)

	    _ldflags="${LDFLAGS}"
	    _libs="${LIBS}"
	    LDFLAGS="${LDFLAGS} ${ICONV_LIBS}"
	    LIBS="${LIBS} -liconv"

	    AC_TRY_LINK([#include <stdlib.h>
#include <iconv.h>],[
iconv_t cd = iconv_open ("","");
iconv (cd, NULL, NULL, NULL, NULL);],[
		AC_MSG_RESULT(yes)
		WITH_ICONV=1
		ICONV_LIBS="${ICONV_LIBS} -liconv"
		LIBS="${_libs}"
		LDFLAGS="${_ldflags}"],[
		AC_MSG_RESULT(no)
		LIBS="${_libs}"
		LDFLAGS="${_ldflags}"])]))
fi

if test "$WITH_ICONV" = "1"; then
    AC_DEFINE(HAVE_ICONV, 1, [Define if you have the iconv() function.])
fi

AC_SUBST(WITH_ICONV)

LIBXLS_LIBS="-lxls $ICONV_LIBS $LIBS"

AC_SUBST(LIBXLS_LIBS)

AC_CONFIG_FILES([Makefile include/Makefile include/libxls/Makefile src/Makefile test/Makefile])
AC_OUTPUT
